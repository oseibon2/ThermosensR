#include <SPI.h>
#include <SD.h>
//include <Wire.h>  // i commented this because there's no Wire.h library/
#include "RTClib.h"
#include "MLX90615.h"   // i called following libraries//
#include <I2cMaster.h>
#include <OneWire.h>
#include <DallasTemperature.h>

// A simple data logger for the Arduino analog pins

// how many milliseconds between grabbing data and logging it. 1000 ms is once a second
#define LOG_INTERVAL  1000 // mills between entries (reduce to take more/faster data)

// how many milliseconds before writing the logged data permanently to disk
// set it to the LOG_INTERVAL to write each time (safest)
// set it to 10*LOG_INTERVAL to write all data every 10 datareads, you could lose up to 
// the last 10 reads if power is lost but it uses less power and is much faster!
#define SYNC_INTERVAL 1000 // mills between calls to flush() - to write data to the card
uint32_t syncTime = 0; // time of last sync()

#define ECHO_TO_SERIAL   1 // echo data to serial port
#define WAIT_TO_START    0 // Wait for serial input in setup()

// the digital pins that connect to the LEDs
#define redLEDpin 5         // i changed the pin number from 2/
#define greenLEDpin 6       // i changed the pin number from 3/

#define SDA_PIN 3   //define the SDA pin // I included the ff definitions/
#define SCL_PIN 2   //define the SCL pin
#define ONE_WIRE_BUS 4

SoftI2cMaster i2c(SDA_PIN, SCL_PIN); // I inserted this/
MLX90615 mlx90615(DEVICE_ADDR, &i2c);
OneWire ourWire(ONE_WIRE_BUS);
DallasTemperature sensors(&ourWire);

RTC_PCF8523 RTC; // define the Real Time Clock object // I included this but its not highlighted/

// for the data logging shield, we use digital pin 10 for the SD cs line
const int chipSelect = 10;

// the logging file
File logfile;

void error(char *str)
{
  Serial.print("error: ");
  Serial.println(str);
  
  // red LED indicates error
  digitalWrite(redLEDpin, HIGH);

  while(1);
}

void setup(void)
{
  Serial.begin(9600);
  Serial.println();
  
  // use debugging LEDs
  pinMode(redLEDpin, OUTPUT);
  pinMode(greenLEDpin, OUTPUT);
  
#if WAIT_TO_START
  Serial.println("Type any character to start");
  while (!Serial.available());
#endif //WAIT_TO_START

  // initialize the SD card
  Serial.print("Initializing SD card...");
  // make sure that the default chip select pin is set to
  // output, even if you don't use it:
  pinMode(10, OUTPUT);
  
  // see if the card is present and can be initialized:
  if (!SD.begin(chipSelect)) {
    error("Card failed, or not present");
  }
  Serial.println("card initialized.");
  
  // create a new file
  char filename[] = "LOGGER00.CSV";
  for (uint8_t i = 0; i < 100; i++) {
    filename[6] = i/10 + '0';
    filename[7] = i%10 + '0';
    if (! SD.exists(filename)) {
      // only open a new file if it doesn't exist
      logfile = SD.open(filename, FILE_WRITE); 
      break;  // leave the loop!
    }
  }
  
  if (! logfile) {
    error("couldnt create file");
  }
  
  Serial.print("Logging to: ");
  Serial.println(filename);

  // connect to RTC
 // Wire.begin();  I commneted this/
  if (!RTC.begin()) {
    logfile.println("RTC failed");
#if ECHO_TO_SERIAL
    Serial.println("RTC failed");
#endif  //ECHO_TO_SERIAL
  }
  

  logfile.println("millis,stamp,datetime,leaftemp,ambtemp,soiltemp");   // i added leaftemp,ambtemp,soiltemp. original was (millis,stamp,datetime,light,temp,vcc) 
#if ECHO_TO_SERIAL
  Serial.println("millis,stamp,datetime,leaftemp,ambtemp,soiltemp");
#endif //ECHO_TO_SERIAL
 
}

void loop(void)
{
  DateTime now;

  // delay for the amount of time we want between readings
  delay((LOG_INTERVAL -1) - (millis() % LOG_INTERVAL));
  
  digitalWrite(greenLEDpin, HIGH);
  
  // log milliseconds since starting
  uint32_t m = millis();
  logfile.print(m);           // milliseconds since start
  logfile.print(", ");    
#if ECHO_TO_SERIAL
  Serial.print(m);         // milliseconds since start
  Serial.print(", ");  
#endif

  // fetch the time
  now = RTC.now();
  // log time
  logfile.print(now.unixtime()); // seconds since 1/1/1970
  logfile.print(", ");
  logfile.print('"');
  logfile.print(now.year(), DEC);
  logfile.print("/");
  logfile.print(now.month(), DEC);
  logfile.print("/");
  logfile.print(now.day(), DEC);
  logfile.print(" ");
  logfile.print(now.hour(), DEC);
  logfile.print(":");
  logfile.print(now.minute(), DEC);
  logfile.print(":");
  logfile.print(now.second(), DEC);
  logfile.print('"');
#if ECHO_TO_SERIAL
  Serial.print(now.unixtime()); // seconds since 1/1/1970
  Serial.print(", ");
  Serial.print('"');
  Serial.print(now.year(), DEC);
  Serial.print("/");
  Serial.print(now.month(), DEC);
  Serial.print("/");
  Serial.print(now.day(), DEC);
  Serial.print(" ");
  Serial.print(now.hour(), DEC);
  Serial.print(":");
  Serial.print(now.minute(), DEC);
  Serial.print(":");
  Serial.print(now.second(), DEC);
  Serial.print('"');
#endif //ECHO_TO_SERIAL
  
  logfile.print(", ");    
  logfile.print(mlx90615.getTemperature(MLX90615_OBJECT_TEMPERATURE));
  logfile.print(", ");    
  logfile.print(mlx90615.getTemperature(MLX90615_AMBIENT_TEMPERATURE));
#if ECHO_TO_SERIAL
  Serial.print(", ");   
  Serial.print(mlx90615.getTemperature(MLX90615_OBJECT_TEMPERATURE));
  Serial.print(", ");    
  Serial.print(mlx90615.getTemperature(MLX90615_AMBIENT_TEMPERATURE));
#endif //ECHO_TO_SERIAL

  sensors.begin();   // start dallas temperature sensor (soiltemp)/

  sensors.requestTemperatures(); //sends command to request temperature/
  logfile.print(", ");
  logfile.print(sensors.getTempCByIndex(0));
#if ECHO_TO_SERIAL
  Serial.print(", ");   
  Serial.print(sensors.getTempCByIndex(0));
#endif // ECHO_TO_SERIAL

  logfile.println();
#if ECHO_TO_SERIAL
  Serial.println();
#endif // ECHO_TO_SERIAL

  digitalWrite(greenLEDpin, LOW);

  // Now we write data to disk! Don't sync too often - requires 2048 bytes of I/O to SD card
  // which uses a bunch of power and takes time
  if ((millis() - syncTime) < SYNC_INTERVAL) return;
  syncTime = millis();
  
  // blink LED to show we are syncing data to the card & updating FAT!
  digitalWrite(redLEDpin, HIGH);
  logfile.flush();
  digitalWrite(redLEDpin, LOW);
  
}
